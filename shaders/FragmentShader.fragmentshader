#version 330 core

in vec2 UV;
in vec3 fragmentColor;
in vec3 Normal;
in vec3 FragPos;

out vec3 color;

uniform sampler2D myTextureSampler;
uniform int prevColor; // 0: normal 1: fragmentColor, 2: textureColor
uniform int currentColor; // 0: normal 1: fragmentColor, 2: textureColor
uniform vec3 lightPos;
uniform vec3 lightColor;
uniform vec3 ambientMaterial;
uniform vec3 diffuseMaterial;
uniform vec3 specularMaterial;
uniform float shininess;
uniform float mixFactor;

void main()
{
    vec3 lightColor = vec3(1.0, 1.0, 1.0);
    vec3 ambient = ambientMaterial * lightColor;
    vec3 norm = normalize(Normal);
    vec3 lightDir = normalize(lightPos - FragPos);

    // lambertian reflection model
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = diffuseMaterial * diff * lightColor;

    // phong reflection model
    vec3 viewDir = normalize(-FragPos);
    vec3 reflectDir = reflect(-lightDir, norm);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
    vec3 specular = specularMaterial * spec * lightColor;

    vec3 result = ambient + diffuse + specular;
    vec3 textureColor = texture(myTextureSampler, UV).rgb;
    vec3 array[3];
    array[0] = vec3(1.0, 1.0, 1.0);
    array[1] = fragmentColor;
    array[2] = textureColor;
    result *= mix(array[prevColor], array[currentColor], mixFactor);
    color = result;
}